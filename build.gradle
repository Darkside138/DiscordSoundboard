buildscript {
    ext {
        springBootVersion = '3.5.7'
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
        classpath("org.springframework.boot:spring-boot-starter-jersey:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'distribution'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
    id 'application'
    id 'com.github.node-gradle.node' version '7.0.1'
}

repositories {
    mavenCentral()
    maven {
        name = 'm2-dv8tion'
        url = 'https://m2.dv8tion.net/releases'
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = "https://m2.chew.pro/releases"
    }
}

processResources {
    filesMatching('application.yml') {
        expand(project.properties)
    }
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.named("bootJar") {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                'Implementation-Version': projectVersion
    }
}

dependencies {
    implementation 'net.dv8tion:JDA:6.1.1'
    //implementation 'com.github.walkyst:lavaplayer-fork:1.4.0'
    implementation 'com.sedmelluq:lavaplayer:1.3.78'
    implementation 'com.github.aikaterna:lavaplayer-natives:original-SNAPSHOT'
    implementation 'pw.chew:jda-chewtils:1.24.1'

    implementation 'commons-io:commons-io:2.14.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-jersey'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'

    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "org.mockito:mockito-core:4.4.0"
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

node {
    version = '20.11.0'
    npmVersion = '10.2.4'
    download = true
    nodeProjectDir = file("${project.projectDir}/src/frontend")
}

npm_install {
    outputs.dir("${project.projectDir}/src/frontend/node_modules")
}

// Task to build the React app
tasks.register("buildFrontend", NpmTask) {
    description = 'Build the frontend application'
    args = ['run', 'build']
    dependsOn npm_install  // Use the plugin's built-in task
}

// Task to copy built files to Spring Boot static directory
tasks.register("copyFrontend", Copy) {
    description = 'Copy built frontend files to Spring Boot static resources'
    dependsOn buildFrontend
    from "${project.projectDir}/src/frontend/build"
    into "${layout.buildDirectory.get().asFile}/resources/main/static"
    from "${project.projectDir}/public/favicon.png"
    into "${layout.buildDirectory.get().asFile}/resources/main/static"
}

// Make processResources depend on copyFrontend
processResources.dependsOn copyFrontend

// Clean frontend build
tasks.register("cleanFrontend", Delete) {
    delete "${project.projectDir}/src/frontend/build"
    delete "${project.projectDir}/src/frontend/node_modules"
}

clean.dependsOn cleanFrontend

processResources {
    exclude 'app.properties'
}

//Run tasks -> distributions -> assembleBootDist to create a release zip
bootDistZip {
    archiveFileName = 'DiscordSoundboard.zip'
    into ("DiscordSoundboard/bin") {
        from ('src/main/resources/application.properties')
        from ("${layout.buildDirectory.get().asFile}/resources/main/application.yml")
    }
    into("DiscordSoundboard/bin/static") {
        from('src/main/webapp')
        from ("${layout.buildDirectory.get()}/resources/main/static/")
    }
    into("DiscordSoundboard/bin/sounds") {
        from('sounds')
    }
    into("DiscordSoundboard/bin/") {
        from("${layout.buildDirectory.get().asFile}/libs/DiscordSoundboard.jar")
    }
}

tasks.register("renameDist") {
    doLast {
    new File("${layout.buildDirectory.get().asFile}/distributions/DiscordSoundboard.zip")
            .renameTo("${layout.buildDirectory.get().asFile}/distributions/DiscordSoundboard-"+projectVersion+".zip")
    }
}

assembleBootDist.dependsOn test

bootDistZip.finalizedBy renameDist

tasks.register("bumpPatchVersion") {
    def targetFile = file("gradle.properties")
    def linePrefix = "projectVersion"

    inputs.file targetFile
    outputs.file targetFile

    doLast {
        if (!targetFile.exists()) {
            throw new GradleException("File not found: $targetFile")
        }

        def updatedContent = targetFile.readLines().collect { line ->
            if (line.startsWith(linePrefix)) {
                def parts = line.split("\\s*=\\s*")
                def matcher = (parts[1] =~ /(\d+)\.(\d+)\.(\d+)/)
                if (!matcher.matches()) {
                    throw new GradleException("Version format must be MAJOR.MINOR.PATCH, found: $version")
                }

                // Explicitly convert to String, then to int
                def major = matcher[0][1].toString().toInteger()
                def minor = matcher[0][2].toString().toInteger()
                def patch = matcher[0][3].toString().toInteger() + 1

                def newVersion = "projectVersion=$major.$minor.$patch"

                newVersion
            } else {
                line
            }
        }.join(System.lineSeparator())

        targetFile.text = updatedContent
        println "Updated line starting with '$linePrefix' in $targetFile"
    }
}

